;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; A Test Game for a Math Package
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role robot)

(<= (base (test ?task ?result))
    (task ?task)
    (result ?result))
(base finished)

(<= (input robot (run ?task))
    (task ?task))
(input robot quit)

(<= (init (test ?task waiting))
    (task ?task))

(<= (legal robot (run ?task))
    (true (test ?task waiting)))
(<= (legal robot quit)
    (not running))

(<= (next (test ?task1 ?result))
    (does robot (run ?task2))
    (true (test ?task1 ?result))
    (distinct ?task1 ?task2))

(<= (next (test ?task success))
    (does robot (run ?task))
    (success ?task))

(<= (next (test ?task failure))
    (does robot (run ?task))
    (not (success ?task)))

(<= (next finished)
    (does robot quit))
    
(<= terminal
    (true finished))

(<= (goal robot 100)
    (true (test ?name success)))

(<= (goal robot 0)
    (not (goal robot 100)))

;;; Tasks to test operations

(task successor)
(task num_b)
(task shiftleft)
(task shiftright)
(task binary_adder)
(task lt)
(task convert_radix)
;(task adder)
;(task multiplier)
(task plus)
(task minus)
(task multiplication)

(<= (success successor)
    (succ 1 0 1 0 1 0 1 1)
    (succ 1 0 1 f 1 0 2 0)
    (not (succ 1 0 0 2 1 0 0 4)))

(<= (success num_b)
    (num_b i o o i o i i o o i i o i o i o)
    (num_b i i o o i o o i i i o i i o i i))

(<= (success convert_radix)
    (conv_radix o i i o i i o o o i o i i i o o 6 c 5 c)
    (conv_radix i o i o o o o i i o i o i o o i a 1 a 9))

(<= (success shiftleft)
    (sl_b o i i o i i o o o i o i i i o o 0 o i i o i i o o o i o i i i o o)
    (sl_b o i i o i i o o o i o i i i o o 1 i i o i i o o o i o i i i o o o)
    (sl_b i o i o o o o i i o i o i o o i 2 i o o o o i i o i o i o o i o o)
    (sl_b i o i o o o o i i o i o i o o i 5 o o i i o i o i o o i o o o o o))
    
(<= (success shiftright)
    (sr_b o i i o i i o o o i o i i i o o 0 o i i o i i o o o i o i i i o o)
    (sr_b o i i o i i o o o i o i i i o o 1 o o i i o i i o o o i o i i i o)
    (sr_b i o i o o o o i i o i o i o o i 2 o o i o i o o o o i i o i o i o)
    (sr_b i o i o o o o i i o i o i o o i 5 o o o o o i o i o o o o i i o i))

(<= (success binary_adder)
    (adder o o o o o)
    (adder o i o i o)
    (adder i o o i o)
    (adder o i i o i)
    (adder i i i i i))

(<= (success adder)
    (adder 0 0 0 0 0)
    (adder 1 1 0 2 0)
    (adder 1 f 0 0 1)
    (adder 1 f f f 1)
    (adder f f f d 2)
    (not (adder 4 a 4 3 1))
    (not (adder 1 k 0 0 1)))

(<= (success multiplier)
    (multiplier 0 0 0 0)
    (multiplier 1 1 0 1)
    (multiplier f 2 1 e)
    (multiplier f f e 1)
    (not (multiplier 4 a 8 b))
    (not (multiplier n 6 n 4)))

(<= (success lt)
    (lt 0 0 0 0 0 0 0 1)
    (lt 0 0 0 0 0 0 4 0)
    (not (lt 8 4 2 0 4 0 2 a)))

(<= (success plus)
    (plus 0 0 0 0 0 0 0 0 0 0 0 0 o)
    (plus f 2 8 4 0 2 c 5 f 5 4 9 o)
    (plus 0 0 0 0 f f f f f f f f o)
    (not (plus 2 5 6 3 3 4 0 4 5 3 2 0 o))
    (not (plus 2 f 8 g m 33 a 2 0 3 4 b o))
    (plus f 3 0 4 b a 2 0 a d 2 4 i))

(<= (success minus)
    (minus 0 0 0 0 0 0 0 0 0 0 0 0 o)
    (minus 0 f 0 1 0 e 0 1 0 1 0 0 o)
    (not (minus 2 4 8 3 1 9 b c 3 a f 0 i))
    (not (minus g a 3 0 j t 0 0 h b a c o))
    (minus 0 0 0 1 f f f f 0 0 0 2 i))

(<= (success multiplication)
    (mul 0 0 0 0 0 0 0 0 0 0 0 0 o)
    (mul 0 1 a 4 0 0 4 a 8 b e 8 o)
    (not (mul 2 5 2 8 0 0 3 a 8 b c e o))
    (not (mul g a 7 0 3 b a k j j b c o))
    (mul b 5 0 3 f 3 a a 7 c 5 e i))

;;; General Functions

(<= running
    (true (test ?name waiting)))

(result waiting)
(result success)
(result failure)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; A Common Package of Mathematics
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Notation
;;;    a.b = ?i1 ?i2 ... (Let (a ?i1 ?i2 ...))
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Numbers: 0-65535
;;; - Hexadecimal 4 digit (16bit)
;;;    (num i.3 i.2 i.1 i.0) = i.0 * 2^4 + i.1 * 2^8 + i.2 * 2^12 + i.3 * 2^16
;;;    (num nan nan nan nan) = NaN (Not a number including overflow)
;;;    (minnum num.i) -> num.i == Zero
;;;    (maxnum num.i) -> num.i == 0xFFFF
;;;    (nan num.i) -> num.i == NaN
;;;
;;; Arithmetic Operations: + - * / %
;;;    (plus num.a num.b num.sum)     -> num.a + num.b = num.sum
;;;    (minus num.a num.b num.diff)   -> num.a - num.b = num.diff
;;;    (mul num.a num.b num.product)  -> num.a * num.b = num.product
;;;    (div num.a num.b num.quotient) -> num.a / num.b = num.quotient
;;;    (rem num.a num.b num.surplus)  -> num.a % num.b = num.surplus
;;;
;;; Comparison Operations: < > <= >= == !=
;;;    (lt num.a num.b) -> num.a < num.b
;;;    (gt num.a num.b) -> num.a > num.b
;;;    (le num.a num.b) -> num.a <= num.b
;;;    (ge num.a num.b) -> num.a >= num.b
;;;    (eq num.a num.b) -> num.a == num.b
;;;    (ne num.a num.b) -> num.a != num.b
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; Hex Digits

(i 0)
(i 1)
(i 2)
(i 3)
(i 4)
(i 5)
(i 6)
(i 7)
(i 8)
(i 9)
(i a)
(i b)
(i c)
(i d)
(i e)
(i f)

;;; Binary Digits

(binary o)
(binary i)

;;; Numbers / NaN

(<= (num ?i3 ?i2 ?i1 ?i0)
    (i ?i0)
    (i ?i1)
    (i ?i2)
    (i ?i3))
(num nan nan nan nan)

(<= (num_b ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0)
    (binary ?b15)
    (binary ?b14)
    (binary ?b13)
    (binary ?b12)
    (binary ?b11)
    (binary ?b10)
    (binary ?b9)
    (binary ?b8)
    (binary ?b7)
    (binary ?b6)
    (binary ?b5)
    (binary ?b4)
    (binary ?b3)
    (binary ?b2)
    (binary ?b1)
    (binary ?b0))

(radix_table o o o o 0)
(radix_table o o o i 1)
(radix_table o o i o 2)
(radix_table o o i i 3)
(radix_table o i o o 4)
(radix_table o i o i 5)
(radix_table o i i o 6)
(radix_table o i i i 7)
(radix_table i o o o 8)
(radix_table i o o i 9)
(radix_table i o i o a)
(radix_table i o i i b)
(radix_table i i o o c)
(radix_table i i o i d)
(radix_table i i i o e)
(radix_table i i i i f)

(zero 0 0 0 0 i)
(<= (zero ?i3 ?i2 ?i1 ?i0 o)
    (num ?i3 ?i2 ?i1 ?i0)
    (not (zero ?i3 ?i2 ?i1 ?i0 i)))
(nan nan nan nan nan)

(<= (odd_num ?i3 ?i2 ?i1 ?i0)
    (radix_table ?b3 ?b2 ?b1 i ?i0)
    (num ?i3 ?i2 ?i1 ?i0))
(<= (even_num ?i3 ?i2 ?i1 ?i0)
    (radix_table ?b3 ?b2 ?b1 o ?i0)
    (num ?i3 ?i2 ?i1 ?i0))

;;; Successors

(succi 0 1)
(succi 1 2)
(succi 2 3)
(succi 3 4)
(succi 4 5)
(succi 5 6)
(succi 6 7)
(succi 7 8)
(succi 8 9)
(succi 9 a)
(succi a b)
(succi b c)
(succi c d)
(succi d e)
(succi e f)

(<= (succ ?i3 ?i2 ?i1 ?i0 ?i3 ?i2 ?i1 ?j0)
    (num ?i3 ?i2 ?i1 ?i0)
    (num ?i3 ?i2 ?i1 ?j0)
    (succi ?i0 ?j0))
(<= (succ ?i3 ?i2 ?i1 f ?i3 ?i2 ?j1 0)
    (num ?i3 ?i2 ?i1 f)
    (num ?i3 ?i2 ?j1 0)
    (succi ?i1 ?j1))
(<= (succ ?i3 ?i2 f f ?i3 ?j2 0 0)
    (num ?i3 ?i2 f f)
    (num ?i3 ?j2 0 0)
    (succi ?i2 ?j2))
(<= (succ ?i3 f f f ?j3 0 0 0)
    (num ?i3 f f f)
    (num ?j3 0 0 0)
    (succi ?i3 ?j3))

;; Logical Operations

(_or o o o)
(_or o i i)
(_or i o i)
(_or i i i)

(_and o o o)
(_and o i o)
(_and i o o)
(_and i i i)

(_not o i)
(_not i o)

(_nor o o i)
(_nor o i o)
(_nor i o o)
(_nor i i o)

(_nand o o i)
(_nand o i i)
(_nand i o i)
(_nand i i o)

(_xor o o o)
(_xor o i i)
(_xor i o i)
(_xor i i o)

;; Converters

; Radix (binary <-> hex)

(<= (conv_radix ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0 ?n3 ?n2 ?n1 ?n0)
    (radix_table ?b15 ?b14 ?b13 ?b12 ?n3)
    (radix_table ?b11 ?b10 ?b9 ?b8 ?n2)
    (radix_table ?b7 ?b6 ?b5 ?b4 ?n1)
    (radix_table ?b3 ?b2 ?b1 ?b0 ?n0))

; Complement on two    

(<= (conv_complement ?i15 ?i14 ?i13 ?i12 ?i11 ?i10 ?i9 ?i8 ?i7 ?i6 ?i5 ?i4 ?i3 ?i2 ?i1 ?i0 ?j15 ?j14 ?j13 ?j12 ?j11 ?j10 ?j9 ?j8 ?j7 ?j6 ?j5 ?j4 ?j3 ?j2 ?j1 ?j0)
    (_not ?i15 ?ia15)
    (_not ?i14 ?ia14)
    (_not ?i13 ?ia13)
    (_not ?i12 ?ia12)
    (_not ?i11 ?ia11)
    (_not ?i10 ?ia10)
    (_not ?i9 ?ia9)
    (_not ?i8 ?ia8)
    (_not ?i7 ?ia7)
    (_not ?i6 ?ia6)
    (_not ?i5 ?ia5)
    (_not ?i4 ?ia4)
    (_not ?i3 ?ia3)
    (_not ?i2 ?ia2)
    (_not ?i1 ?ia1)
    (_not ?i0 ?ia0)
    (plus_b ?ia15 ?ia14 ?ia13 ?ia12 ?ia11 ?ia10 ?ia9 ?ia8 ?ia7 ?ia6 ?ia5 ?ia4 ?ia3 ?ia2 ?ia1 ?ia0 o o o o o o o o o o o o o o o i o ?j15 ?j14 ?j13 ?j12 ?j11 ?j10 ?j9 ?j8 ?j7 ?j6 ?j5 ?j4 ?j3 ?j2 ?j1 ?j0 ?overflow))

;; Shift Operations

; Logical Right-Shift (Binary)
(<= (sr_b ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0 0 ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0)
    (num_b ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0))
(<= (sr_b ?a15 ?a14 ?a13 ?a12 ?a11 ?a10 ?a9 ?a8 ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?n ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0)
    (binary ?a0)
    (succi ?nm ?n)
    (sr_b o ?a15 ?a14 ?a13 ?a12 ?a11 ?a10 ?a9 ?a8 ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?nm ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0))

; Logical Right-Shift (Hex)
(<= (sr ?i3 ?i2 ?i1 ?i0 ?n ?j3 ?j2 ?j1 ?j0)
    (conv_radix ?ib15 ?ib14 ?ib13 ?ib12 ?ib11 ?ib10 ?ib9 ?ib8 ?ib7 ?ib6 ?ib5 ?ib4 ?ib3 ?ib2 ?ib1 ?ib0 ?i3 ?i2 ?i1 ?i0)
    (conv_radix ?jb15 ?jb14 ?jb13 ?jb12 ?jb11 ?jb10 ?jb9 ?jb8 ?jb7 ?jb6 ?jb5 ?jb4 ?jb3 ?jb2 ?jb1 ?jb0 ?j3 ?j2 ?j1 ?j0)
    (sr_b ?ib15 ?ib14 ?ib13 ?ib12 ?ib11 ?ib10 ?ib9 ?ib8 ?ib7 ?ib6 ?ib5 ?ib4 ?ib3 ?ib2 ?ib1 ?ib0 ?n ?jb15 ?jb14 ?jb13 ?jb12 ?jb11 ?jb10 ?jb9 ?jb8 ?jb7 ?jb6 ?jb5 ?jb4 ?jb3 ?jb2 ?jb1 ?jb0))

; Logical Left-Shift (Binary)
(<= (sl_b ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0 0 ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0)
    (num_b ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0))
(<= (sl_b ?a15 ?a14 ?a13 ?a12 ?a11 ?a10 ?a9 ?a8 ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?n ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0)
    (succi ?nm ?n)
    (binary ?a15)
    (sl_b ?a14 ?a13 ?a12 ?a11 ?a10 ?a9 ?a8 ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 o ?nm ?b15 ?b14 ?b13 ?b12 ?b11 ?b10 ?b9 ?b8 ?b7 ?b6 ?b5 ?b4 ?b3 ?b2 ?b1 ?b0))

; Logical Left-Shift (Hex)
(<= (sl ?i3 ?i2 ?i1 ?i0 ?n ?j3 ?j2 ?j1 ?j0)
    (conv_radix ?ib15 ?ib14 ?ib13 ?ib12 ?ib11 ?ib10 ?ib9 ?ib8 ?ib7 ?ib6 ?ib5 ?ib4 ?ib3 ?ib2 ?ib1 ?ib0 ?i3 ?i2 ?i1 ?i0)
    (conv_radix ?jb15 ?jb14 ?jb13 ?jb12 ?jb11 ?jb10 ?jb9 ?jb8 ?jb7 ?jb6 ?jb5 ?jb4 ?jb3 ?jb2 ?jb1 ?jb0 ?j3 ?j2 ?j1 ?j0)
    (sl_b ?ib15 ?ib14 ?ib13 ?ib12 ?ib11 ?ib10 ?ib9 ?ib8 ?ib7 ?ib6 ?ib5 ?ib4 ?ib3 ?ib2 ?ib1 ?ib0 ?n ?jb15 ?jb14 ?jb13 ?jb12 ?jb11 ?jb10 ?jb9 ?jb8 ?jb7 ?jb6 ?jb5 ?jb4 ?jb3 ?jb2 ?jb1 ?jb0))

;;; Arithmetic Operations

; 四則演算は暗黙的に16進数から2進数に変換して処理することにしよう

;; Full Adder (Binary)
; (adder ?a ?b ?x ?s ?c) -> ?a + ?b + ?x = ?c?s
; ?a: input A | ?b: input B | ?x: carry-input | ?s: sum-output | ?c: carry-output

(<= (adder ?a ?b ?x ?s ?c)
    (_xor ?a ?b ?s1)
    (_xor ?s1 ?x ?s)
    (_and ?a ?b ?c1)
    (_and ?s1 ?x ?c2)
    (_or ?c1 ?c2 ?c))

;; Comparator
; (comparator ?i ?j) -> (?i < ?j)

(<= (comparator ?i ?j)
    (succi ?i ?j))
(<= (comparator ?h ?j)
    (succi ?i ?j)
    (comparator ?h ?i))

;;; Comparison Operations

;; i < j

(<= (lt ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0)
    (comparator ?i3 ?j3)
    (num ?i3 ?i2 ?i1 ?i0)
    (num ?j3 ?j2 ?j1 ?j0))
(<= (lt ?i3 ?i2 ?i1 ?i0 ?i3 ?j2 ?j1 ?j0)
    (comparator ?i2 ?j2)
    (num ?i3 ?i2 ?i1 ?i0)
    (num ?i3 ?j2 ?j1 ?j0))
(<= (lt ?i3 ?i2 ?i1 ?i0 ?i3 ?i2 ?j1 ?j0)
    (comparator ?i1 ?j1)
    (num ?i3 ?i2 ?i1 ?i0)
    (num ?i3 ?i2 ?j1 ?j0))
(<= (lt ?i3 ?i2 ?i1 ?i0 ?i3 ?i2 ?i1 ?j0)
    (comparator ?i0 ?j0)
    (num ?i3 ?i2 ?i1 ?i0)
    (num ?i3 ?i2 ?i1 ?j0))

;; i <= j
(<= (le ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0)
    (lt ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0))
(<= (le ?i3 ?i2 ?i1 ?i0 ?i3 ?i2 ?i1 ?i0)
    (num ?i3 ?i2 ?i1 ?i0))

;; i > j
(<= (gt ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0)
    (lt ?j3 ?j2 ?j1 ?j0 ?i3 ?i2 ?i1 ?i0))

;; i >= j
(<= (ge ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0)
    (gt ?j3 ?j2 ?j1 ?j0 ?i3 ?i2 ?i1 ?i0))

;; i == j
(<= (eq ?i3 ?i2 ?i1 ?i0 ?i3 ?i2 ?i1 ?i0)
    (num ?i3 ?i2 ?i1 ?i0))

;; i != j
(<= (ne ?i3 ?i2 ?i1 ?i0 ?i3 ?i2 ?i1 ?i0)
    (not (eq ?i3 ?i2 ?i1 ?i0 ?i3 ?i2 ?i1 ?i0))
    (num ?i3 ?i2 ?i1 ?i0))

;; Addition
; i + j = s
; (?overflow == i) -> sum overflows

(<= (plus ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 ?s3 ?s2 ?s1 ?s0 ?overflow)
    (plus_ ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 o ?s3 ?s2 ?s1 ?s0 ?overflow))

(<= (plus_ ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 ?cb0 ?s3 ?s2 ?s1 ?s0 ?overflow)
    (conv_radix ?ib15 ?ib14 ?ib13 ?ib12 ?ib11 ?ib10 ?ib9 ?ib8 ?ib7 ?ib6 ?ib5 ?ib4 ?ib3 ?ib2 ?ib1 ?ib0 ?i3 ?i2 ?i1 ?i0)
    (conv_radix ?jb15 ?jb14 ?jb13 ?jb12 ?jb11 ?jb10 ?jb9 ?jb8 ?jb7 ?jb6 ?jb5 ?jb4 ?jb3 ?jb2 ?jb1 ?jb0 ?j3 ?j2 ?j1 ?j0)
    (conv_radix ?sb15 ?sb14 ?sb13 ?sb12 ?sb11 ?sb10 ?sb9 ?sb8 ?sb7 ?sb6 ?sb5 ?sb4 ?sb3 ?sb2 ?sb1 ?sb0 ?s3 ?s2 ?s1 ?s0)
    (plus_b ?ib15 ?ib14 ?ib13 ?ib12 ?ib11 ?ib10 ?ib9 ?ib8 ?ib7 ?ib6 ?ib5 ?ib4 ?ib3 ?ib2 ?ib1 ?ib0 ?jb15 ?jb14 ?jb13 ?jb12 ?jb11 ?jb10 ?jb9 ?jb8 ?jb7 ?jb6 ?jb5 ?jb4 ?jb3 ?jb2 ?jb1 ?jb0 ?cb0 ?sb15 ?sb14 ?sb13 ?sb12 ?sb11 ?sb10 ?sb9 ?sb8 ?sb7 ?sb6 ?sb5 ?sb4 ?sb3 ?sb2 ?sb1 ?sb0 ?overflow))

(<= (plus_b ?ib15 ?ib14 ?ib13 ?ib12 ?ib11 ?ib10 ?ib9 ?ib8 ?ib7 ?ib6 ?ib5 ?ib4 ?ib3 ?ib2 ?ib1 ?ib0 ?jb15 ?jb14 ?jb13 ?jb12 ?jb11 ?jb10 ?jb9 ?jb8 ?jb7 ?jb6 ?jb5 ?jb4 ?jb3 ?jb2 ?jb1 ?jb0 ?cb0 ?sb15 ?sb14 ?sb13 ?sb12 ?sb11 ?sb10 ?sb9 ?sb8 ?sb7 ?sb6 ?sb5 ?sb4 ?sb3 ?sb2 ?sb1 ?sb0 ?overflow)
    (adder ?ib0 ?jb0 ?cb0 ?sb0 ?cb1)
    (adder ?ib1 ?jb1 ?cb1 ?sb1 ?cb2)
    (adder ?ib2 ?jb2 ?cb2 ?sb2 ?cb3)
    (adder ?ib3 ?jb3 ?cb3 ?sb3 ?cb4)
    (adder ?ib4 ?jb4 ?cb4 ?sb4 ?cb5)
    (adder ?ib5 ?jb5 ?cb5 ?sb5 ?cb6)
    (adder ?ib6 ?jb6 ?cb6 ?sb6 ?cb7)
    (adder ?ib7 ?jb7 ?cb7 ?sb7 ?cb8)
    (adder ?ib8 ?jb8 ?cb8 ?sb8 ?cb9)
    (adder ?ib9 ?jb9 ?cb9 ?sb9 ?cb10)
    (adder ?ib10 ?jb10 ?cb10 ?sb10 ?cb11)
    (adder ?ib11 ?jb11 ?cb11 ?sb11 ?cb12)
    (adder ?ib12 ?jb12 ?cb12 ?sb12 ?cb13)
    (adder ?ib13 ?jb13 ?cb13 ?sb13 ?cb14)
    (adder ?ib14 ?jb14 ?cb14 ?sb14 ?cb15)
    (adder ?ib15 ?jb15 ?cb15 ?sb15 ?overflow))

;(<= (plus ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 ?s3 ?s2 ?s1 ?s0)
;    (adder ?i0 ?j0 0 ?s0 ?c0)
;    (adder ?i1 ?j1 ?c0 ?s1 ?c1)
;    (adder ?i2 ?j2 ?c1 ?s2 ?c2)
;    (adder ?i3 ?j3 ?c2 ?s3 0))
;(<= (plus ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 nan nan nan nan)
;    (adder ?i0 ?j0 0 ?s0 ?c0)
;    (adder ?i1 ?j1 ?c0 ?s1 ?c1)
;    (adder ?i2 ?j2 ?c1 ?s2 ?c2)
;    (adder ?i3 ?j3 ?c2 ?s3 ?c3)
;    (distinct c3 0))
;(<= (plus ?i3 ?i2 ?i1 ?i0 nan nan nan nan nan nan nan nan)
;    (num ?i3 ?i2 ?i1 ?i0))
;(<= (plus nan nan nan nan ?j3 ?j2 ?j1 ?j0 nan nan nan nan)
;    (num ?j3 ?j2 ?j1 ?j0))

;; Subtraction
; i - j = s
; (?overflow == i) -> the difference overflows

(<= (minus ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 ?d3 ?d2 ?d1 ?d0 ?overflow)
    (plus ?j3 ?j2 ?j1 ?j0 ?d3 ?d2 ?d1 ?d0 ?i3 ?i2 ?i1 ?i0 ?overflow))

; ここまで実装&テスト済み

;; Multiplication
; i * j = s
; (?overflow == i) -> the product overflows

(<= (mul ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 ?p3 ?p2 ?p1 ?p0 ?overflow)
    (conv_radix ?i15_b ?i14_b ?i13_b ?i12_b ?i11_b ?i10_b ?i9_b ?i8_b ?i7_b ?i6_b ?i5_b ?i4_b ?i3_b ?i2_b ?i1_b ?i0_b ?i3 ?i2 ?i1 ?i0)
    (conv_complement ?i15_b ?i14_b ?i13_b ?i12_b ?i11_b ?i10_b ?i9_b ?i8_b ?i7_b ?i6_b ?i5_b ?i4_b ?i3_b ?i2_b ?i1_b ?i0_b ?ia15_b ?ia14_b ?ia13_b ?ia12_b ?ia11_b ?ia10_b ?ia9_b ?ia8_b ?ia7_b ?ia6_b ?ia5_b ?ia4_b ?ia3_b ?ia2_b ?ia1_b ?ia0_b)
    (conv_radix ?ia15_b ?ia14_b ?ia13_b ?ia12_b ?ia11_b ?ia10_b ?ia9_b ?ia8_b ?ia7_b ?ia6_b ?ia5_b ?ia4_b ?ia3_b ?ia2_b ?ia1_b ?ia0_b ?ia3 ?ia2 ?ia1 ?ia0)
    (booth ?i3 ?i2 ?i1 ?i0 0 0 0 0 0 ?ia3 ?ia2 ?ia1 ?ia0 0 0 0 0 0 0 0 0 0 ?j3 ?j2 ?j1 ?j0 0 1 0 ?p3 ?p2 ?p1 ?p0 ?overflow))

(<= (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?p7 ?p6 ?p5 ?p4 ?p3 ?p2 ?p1 ?p0 ?p_lsb 0 0 ?p3 ?p2 ?p1 ?p0 ?overflow)
    (zero ?p7 ?p6 ?p5 ?p4 ?zero)
    (_not ?zero ?overflow)
    (num ?a7 ?a6 ?a5 ?a4)
    (num ?a3 ?a2 ?a1 ?a0)
    (binary ?a_lsb)
    (num ?s7 ?s6 ?s5 ?s4)
    (num ?s3 ?s2 ?s1 ?s0)
    (binary ?s_lsb)
    (num ?p7 ?p6 ?p5 ?p4)
    (num ?p3 ?p2 ?p1 ?p0)
    (binary ?p_lsb))

(<= (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?p7 ?p6 ?p5 ?p4 ?p3 ?p2 ?p1 ?p0 i ?i1 ?i0 ?pk3 ?pk2 ?pk1 ?pk0 ?overflow)
    (succ 0 0 0 ?im 0 0 ?i1 ?i0)
    (radix_table ?p3_b ?p2_b ?p1_b o ?p0)
    (adder i ?a_lsb o ?pi_lsb ?p0c)
    (plus_ ?p3 ?p2 ?p1 ?p0 ?a3 ?a2 ?a1 ?a0 ?p0c ?pi3 ?pi2 ?pi1 ?pi0 ?p4c)
    (plus_ ?p7 ?p6 ?p5 ?p4 ?a7 ?a6 ?a5 ?a4 ?p4c ?pi7 ?pi6 ?pi5 ?pi4 ?p8c)
    (conv_radix ?pi15_b ?pi14_b ?pi13_b ?pi12_b ?pi11_b ?pi10_b ?pi9_b ?pi8_b ?pi7_b ?pi6_b ?pi5_b ?pi4_b ?pi3_b ?pi2_b ?pi1_b ?pi0_b ?pi3 ?pi2 ?pi1 ?pi0)
    (conv_radix ?pi31_b ?pi30_b ?pi29_b ?pi28_b ?pi27_b ?pi26_b ?pi25_b ?pi24_b ?pi23_b ?pi22_b ?pi21_b ?pi20_b ?pi19_b ?pi18_b ?pi17_b ?pi16_b ?pi7 ?pi6 ?pi5 ?pi4)
    (conv_radix o ?pi31_b ?pi30_b ?pi29_b ?pi28_b ?pi27_b ?pi26_b ?pi25_b ?pi24_b ?pi23_b ?pi22_b ?pi21_b ?pi20_b ?pi19_b ?pi18_b ?pi17_b ?pj7 ?pj6 ?pj5 ?pj4)
    (conv_radix ?pi16_b ?pi15_b ?pi14_b ?pi13_b ?pi12_b ?pi11_b ?pi10_b ?pi9_b ?pi8_b ?pi7_b ?pi6_b ?pi5_b ?pi4_b ?pi3_b ?pi2_b ?pi1_b ?pj3 ?pj2 ?pj1 ?pj0)
    (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?pj7 ?pj6 ?pj5 ?pj4 ?pj3 ?pj2 ?pj1 ?pj0 ?pi0_b 0 ?im ?pk3 ?pk2 ?pk1 ?pk0 ?overflow))

(<= (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?p7 ?p6 ?p5 ?p4 ?p3 ?p2 ?p1 ?p0 o ?i1 ?i0 ?pk3 ?pk2 ?pk1 ?pk0 ?overflow)
    (succ 0 0 0 ?im 0 0 ?i1 ?i0)
    (radix_table ?p3_b ?p2_b ?p1_b i ?p0)
    (adder o ?s_lsb o ?pi_lsb ?p0c)
    (plus_ ?p3 ?p2 ?p1 ?p0 ?s3 ?s2 ?s1 ?s0 ?p0c ?pi3 ?pi2 ?pi1 ?pi0 ?p4c)
    (plus_ ?p7 ?p6 ?p5 ?p4 ?s7 ?s6 ?s5 ?s4 ?p4c ?pi7 ?pi6 ?pi5 ?pi4 ?p8c)
    (conv_radix ?pi15_b ?pi14_b ?pi13_b ?pi12_b ?pi11_b ?pi10_b ?pi9_b ?pi8_b ?pi7_b ?pi6_b ?pi5_b ?pi4_b ?pi3_b ?pi2_b ?pi1_b ?pi0_b ?pi3 ?pi2 ?pi1 ?pi0)
    (conv_radix ?pi31_b ?pi30_b ?pi29_b ?pi28_b ?pi27_b ?pi26_b ?pi25_b ?pi24_b ?pi23_b ?pi22_b ?pi21_b ?pi20_b ?pi19_b ?pi18_b ?pi17_b ?pi16_b ?pi7 ?pi6 ?pi5 ?pi4)
    (conv_radix o ?pi31_b ?pi30_b ?pi29_b ?pi28_b ?pi27_b ?pi26_b ?pi25_b ?pi24_b ?pi23_b ?pi22_b ?pi21_b ?pi20_b ?pi19_b ?pi18_b ?pi17_b ?pj7 ?pj6 ?pj5 ?pj4)
    (conv_radix ?pi16_b ?pi15_b ?pi14_b ?pi13_b ?pi12_b ?pi11_b ?pi10_b ?pi9_b ?pi8_b ?pi7_b ?pi6_b ?pi5_b ?pi4_b ?pi3_b ?pi2_b ?pi1_b ?pj3 ?pj2 ?pj1 ?pj0)
    (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?pj7 ?pj6 ?pj5 ?pj4 ?pj3 ?pj2 ?pj1 ?pj0 ?pi0_b 0 ?im ?pk3 ?pk2 ?pk1 ?pk0 ?overflow))

(<= (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?p7 ?p6 ?p5 ?p4 ?p3 ?p2 ?p1 ?p0 o ?i1 ?i0 ?pk3 ?pk2 ?pk1 ?pk0 ?overflow)
    (succ 0 0 0 ?im 0 0 ?i1 ?i0)
    (radix_table ?p3_b ?p2_b ?p1_b o ?p0)
    (conv_radix ?p15_b ?p14_b ?p13_b ?p12_b ?p11_b ?p10_b ?p9_b ?p8_b ?p7_b ?p6_b ?p5_b ?p4_b ?p3_b ?p2_b ?p1_b ?p0_b ?p3 ?p2 ?p1 ?p0)
    (conv_radix ?p31_b ?p30_b ?p29_b ?p28_b ?p27_b ?p26_b ?p25_b ?p24_b ?p23_b ?p22_b ?p21_b ?p20_b ?p19_b ?p18_b ?p17_b ?p16_b ?p7 ?p6 ?p5 ?p4)
    (conv_radix o ?p31_b ?p30_b ?p29_b ?p28_b ?p27_b ?p26_b ?p25_b ?p24_b ?p23_b ?p22_b ?p21_b ?p20_b ?p19_b ?p18_b ?p17_b ?pj7 ?pj6 ?pj5 ?pj4)
    (conv_radix ?p16_b ?p15_b ?p14_b ?p13_b ?p12_b ?p11_b ?p10_b ?p9_b ?p8_b ?p7_b ?p6_b ?p5_b ?p4_b ?p3_b ?p2_b ?p1_b ?pj3 ?pj2 ?pj1 ?pj0)
    (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?pj7 ?pj6 ?pj5 ?pj4 ?pj3 ?pj2 ?pj1 ?pj0 ?p0_b 0 ?im ?pk3 ?pk2 ?pk1 ?pk0 ?overflow))

(<= (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?p7 ?p6 ?p5 ?p4 ?p3 ?p2 ?p1 ?p0 i ?i1 ?i0 ?pk3 ?pk2 ?pk1 ?pk0 ?overflow)
    (succ 0 0 0 ?im 0 0 ?i1 ?i0)
    (radix_table ?p3_b ?p2_b ?p1_b i ?p0)
    (conv_radix ?p15_b ?p14_b ?p13_b ?p12_b ?p11_b ?p10_b ?p9_b ?p8_b ?p7_b ?p6_b ?p5_b ?p4_b ?p3_b ?p2_b ?p1_b ?p0_b ?p3 ?p2 ?p1 ?p0)
    (conv_radix ?p31_b ?p30_b ?p29_b ?p28_b ?p27_b ?p26_b ?p25_b ?p24_b ?p23_b ?p22_b ?p21_b ?p20_b ?p19_b ?p18_b ?p17_b ?p16_b ?p7 ?p6 ?p5 ?p4)
    (conv_radix o ?p31_b ?p30_b ?p29_b ?p28_b ?p27_b ?p26_b ?p25_b ?p24_b ?p23_b ?p22_b ?p21_b ?p20_b ?p19_b ?p18_b ?p17_b ?pj7 ?pj6 ?pj5 ?pj4)
    (conv_radix ?p16_b ?p15_b ?p14_b ?p13_b ?p12_b ?p11_b ?p10_b ?p9_b ?p8_b ?p7_b ?p6_b ?p5_b ?p4_b ?p3_b ?p2_b ?p1_b ?pj3 ?pj2 ?pj1 ?pj0)
    (booth ?a7 ?a6 ?a5 ?a4 ?a3 ?a2 ?a1 ?a0 ?a_lsb ?s7 ?s6 ?s5 ?s4 ?s3 ?s2 ?s1 ?s0 ?s_lsb ?pj7 ?pj6 ?pj5 ?pj4 ?pj3 ?pj2 ?pj1 ?pj0 ?p0_b 0 ?im ?pk3 ?pk2 ?pk1 ?pk0 ?overflow))

;(<= (mul ?i3 ?i2 ?i1 ?i0 0 0 0 0 0 0 0 0)
;    (num ?i3 ?i2 ?i1 ?i0))
;(<= (mul ?i3 ?i2 ?i1 ?i0 ?ip3 ?ip2 ?ip1 ?ip0 ?i3 ?i2 ?i1 ?i0)
;    (succ ?i3 ?i2 ?i1 ?i0 ?ip3 ?ip2 ?ip1 ?ip0))
;(<= (mul ?i3 ?i2 ?i1 ?i0 ?ipp3 ?ipp2 ?ipp1 ?ipp0 ?id3 ?id2 ?id1 ?id0)
;    (plus ?i3 ?i2 ?i1 ?i0 ?i3 ?i2 ?i1 ?i0 ?id3 ?id2 ?id1 ?id0)
;    (succ ?i3 ?i2 ?i1 ?i0 ?ip3 ?ip2 ?ip1 ?ip0)
;    (succ ?ip3 ?ip2 ?ip1 ?ip0 ?ipp3 ?ipp2 ?ipp1 ?ipp0))
;(<= (mul ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 ?s3 ?s2 ?s1 ?s0)
;    (plus ?i3 ?i2 ?i1 ?i0 ?jp3 ?jp2 ?jp1 ?jp0 ?sa3 ?sa2 ?sa1 ?sa0)
;    (plus ?s3 ?s2 ?s1 ?s0 ?i3 ?i2 ?i1 ?i0 ?sa3 ?sa2 ?sa1 ?sa0)
;    (succ ?j3 ?j2 ?j1 ?j0 ?jp3 ?jp2 ?jp1 ?jp0))

;; i / j = s
(<= (div ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 ?s3 ?s2 ?s1 ?s0)
    (mul ?j3 ?j2 ?j1 ?j0 ?s3 ?s2 ?s1 ?s0 ?q3 ?q2 ?q1 ?q0)
    (plus ?q3 ?q2 ?q1 ?q0 ?r3 ?r2 ?r1 ?r0 ?i3 ?i2 ?i1 ?i0)
    (not(nan ?r3 ?r2 ?r1 ?r0))
    (lt ?r3 ?r2 ?r1 ?r0 ?s3 ?s2 ?s1 ?s0)
    (not(minnum ?j3 ?j2 ?j1 ?j0)))
(<= (div ?i3 ?i2 ?i1 ?i0 0 0 0 0 nan nan nan nan)
    (num ?i3 ?i2 ?i1 ?i0))

;; i % j = r
(<= (rem ?i3 ?i2 ?i1 ?i0 ?j3 ?j2 ?j1 ?j0 ?r3 ?r2 ?r1 ?r0)
    (mul ?j3 ?j2 ?j1 ?j0 ?s3 ?s2 ?s1 ?s0 ?q3 ?q2 ?q1 ?q0)
    (plus ?q3 ?q2 ?q1 ?q0 ?r3 ?r2 ?r1 ?r0 ?i3 ?i2 ?i1 ?i0)
    (not(nan ?r3 ?r2 ?r1 ?r0))
    (lt ?r3 ?r2 ?r1 ?r0 ?s3 ?s2 ?s1 ?s0)
    (not(minnum ?j3 ?j2 ?j1 ?j0)))
(<= (rem ?i3 ?i2 ?i1 ?i0 0 0 0 0 nan nan nan nan)
    (num ?i3 ?i2 ?i1 ?i0))

